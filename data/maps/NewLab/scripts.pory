mapscripts NewLab_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: NewLab_EventScript_BallDefault
}
 
script NewLab_EventScript_BallDefault{
    setvar(VAR_SELECT_STARTER_REGION, 0)
    removeobject(5)
    call(Common_EventScript_SetupRivalGfxId)
}

script Start_StarterChoose_Sequence{
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, MeetBirch)
    waitmovement(0)
    msgbox(BeginAdventureText)
    waitmessage
    closemessage
    applymovement(4, ProfMove)
    waitmovement(0)
    setvar(VAR_LITTLEROOT_STATE, 1)
    release
}

script Start_StarterChoose_Sequence2{
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, MeetBirch2)
    waitmovement(0)
    goto(Start_StarterChoose_Sequence)
}

movement MeetBirch{
    walk_up * 3
} 
movement MeetBirch2{
    walk_left
} 
movement ProfMove{
    walk_right * 2
    walk_up
    face_down
}

text BeginAdventureText{
    "{COLOR RED}{SHADOW LIGHT_RED}Birch:{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} Hello {PLAYER}, my name is Prof Birch\n"
    "This is my lab.\p"
    "Today is the day that you will be\n"
    "receiving a Pokemon.\p"
    "{COLOR GREEN}{SHADOW LIGHT_GREEN}Now Choose Wisely,\n"
    "{COLOR BLUE}{SHADOW LIGHT_BLUE}If you would like to see more options\l"
    "come and talk to me.\n"
}

script StopLeaving{
	lockall
	msgbox("You Need To Choose a Pokemon")
	applymovement(OBJ_EVENT_ID_PLAYER, DoNotLeave)
	waitmovement(0)
	releaseall
}
movement DoNotLeave{
    walk_up
}

script NewLab_EventScript_Birch{
    if(!(flag(FLAG_SYS_POKEMON_GET))){
        lock
        faceplayer
        message("Would you like to see more?")
        multichoice(25, 5, MULTI_STARTER_CHOICE, 0)
        closemessage
        if (var(VAR_RESULT) == 0){
            call(StarterFade)
            setvar(VAR_SELECT_STARTER_REGION, 2)
        }
        elif (var(VAR_RESULT) == 1){
            call(StarterFade)
            setvar(VAR_SELECT_STARTER_REGION, 1)
        }
        elif (var(VAR_RESULT) == 2){
            call(StarterFade)
            setvar(VAR_SELECT_STARTER_REGION, 0)
        }
        msgbox("Choose wisely...")
        waitmessage
        release
    }
    else{
        if(flag(FLAG_SYS_POKEDEX_GET) && !(flag(FLAG_SYS_NATIONAL_DEX))){
            msgbox(format("Do you want to upgrade to the National Dex?"), MSGBOX_YESNO)
            if(var(VAR_RESULT) == YES){
                call(UpgradeNationalDex)
            }
            else{
                msgbox("Take care of your Pokemon", MSGBOX_NPC)
            }
        }
        else{
            msgbox("Take care of your Pokemon", MSGBOX_NPC)
        }
    }
    end
}

script StarterFade{
    fadescreen(FADE_TO_BLACK)
    delay(20)
    fadescreen(FADE_FROM_BLACK)
    return
}
 
script NewLab_EventScript_PokeballOne{
    lockall
    setvar(VAR_TEMP_0, 1)
    if(!(flag(FLAG_SYS_POKEMON_GET))){
        if(var(VAR_SELECT_STARTER_REGION) == 0){
            bufferspeciesname(STR_VAR_1, SPECIES_MUDKIP)
            setvar(VAR_STARTER_CHOICE, SPECIES_MUDKIP)
            call(NewLab_EventScript_GiveStarter)
        }
        elif(var(VAR_SELECT_STARTER_REGION) == 1){
            bufferspeciesname(STR_VAR_1, SPECIES_TOTODILE)
            setvar(VAR_STARTER_CHOICE, SPECIES_TOTODILE)
            call(NewLab_EventScript_GiveStarter)
        }
        elif(var(VAR_SELECT_STARTER_REGION) == 2){
            bufferspeciesname(STR_VAR_1, SPECIES_SQUIRTLE)
            setvar(VAR_STARTER_CHOICE, SPECIES_SQUIRTLE)
            call(NewLab_EventScript_GiveStarter)
        }
    }
    else{
        msgbox("Don't Take Another")
    }
    waitmessage
    releaseall
    end
}
 
script NewLab_EventScript_PokeballTwo{
    lockall
    setvar(VAR_TEMP_0, 2)
    if(!(flag(FLAG_SYS_POKEMON_GET))){
        if(var(VAR_SELECT_STARTER_REGION) == 0){
            bufferspeciesname(STR_VAR_1, SPECIES_TREECKO)
            setvar(VAR_STARTER_CHOICE, SPECIES_TREECKO)
            call(NewLab_EventScript_GiveStarter)
        }
        elif(var(VAR_SELECT_STARTER_REGION) == 1){
            bufferspeciesname(STR_VAR_1, SPECIES_CHIKORITA)
            setvar(VAR_STARTER_CHOICE, SPECIES_CHIKORITA)
            call(NewLab_EventScript_GiveStarter)
        }
        elif(var(VAR_SELECT_STARTER_REGION) == 2){
            bufferspeciesname(STR_VAR_1, SPECIES_BULBASAUR)
            setvar(VAR_STARTER_CHOICE, SPECIES_BULBASAUR)
            call(NewLab_EventScript_GiveStarter)
        }
    }
    else{
        msgbox("Don't Take Another")
    }
    waitmessage
    releaseall
    end
}
 
script NewLab_EventScript_PokeballThree{
    lockall
    setvar(VAR_TEMP_0, 3)
    if(!(flag(FLAG_SYS_POKEMON_GET))){
        if(var(VAR_SELECT_STARTER_REGION) == 0){
            bufferspeciesname(STR_VAR_1, SPECIES_TORCHIC)
            setvar(VAR_STARTER_CHOICE, SPECIES_TORCHIC)
            call(NewLab_EventScript_GiveStarter)
        }
        elif(var(VAR_SELECT_STARTER_REGION) == 1){
            bufferspeciesname(STR_VAR_1, SPECIES_CYNDAQUIL)
            setvar(VAR_STARTER_CHOICE, SPECIES_CYNDAQUIL)
            call(NewLab_EventScript_GiveStarter)
        }
        elif(var(VAR_SELECT_STARTER_REGION) == 2){
            bufferspeciesname(STR_VAR_1, SPECIES_CHARMANDER)
            setvar(VAR_STARTER_CHOICE, SPECIES_CHARMANDER)
            call(NewLab_EventScript_GiveStarter)
        }
    }
    else{
        msgbox("Don't Take Another")
    }
    waitmessage
    releaseall
    end
}

script NewLab_EventScript_GiveStarter{
    showmonpic(VAR_STARTER_CHOICE, 10, 3)
    msgbox("Do you want to choose {STR_VAR_1}?", MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES){
        setflag(FLAG_SYS_POKEMON_GET)
        setvar(VAR_LITTLEROOT_STATE, 2)
        givemon(VAR_STARTER_CHOICE, 10, ITEM_MASTER_BALL)
        if(var(VAR_RESULT) == MON_GIVEN_TO_PARTY){
            playfanfare(MUS_OBTAIN_ITEM)
            msgbox("Recieved {STR_VAR_1}")
            removeobject(VAR_TEMP_0)
            call(SetBallFlag)
            call(NicknamePartyMon)
        }
        elif(var(VAR_RESULT) == MON_GIVEN_TO_PC){
            playfanfare(MUS_OBTAIN_ITEM)
            msgbox("Recieved {STR_VAR_1}")
            removeobject(VAR_TEMP_0)
            call(SetBallFlag)
            call(NicknameBoxMon)
            msgbox("Transferred {STR_VAR_1} to PC")
        }
        else{
            hidemonpic
            msgbox("No More Room For Pokemon")
        }
        closemessage
        call(InitiatePokedexSequenceAndRivalBattle)
    }
    else{
        hidemonpic
        msgbox(NewLab_NotChosen)
    }
 
} 

script SetBallFlag{
    if (var(VAR_TEMP_0) == 1){
        setflag(STARTER_BALL_ONE)
        return
    }
    if (var(VAR_TEMP_0) == 2){
        setflag(STARTER_BALL_TWO)
        return
    }
    if (var(VAR_TEMP_0) == 3){
        setflag(STARTER_BALL_THREE)
        return
    }
}
 
script NicknamePartyMon{
    msgbox("Give a nickname?", MSGBOX_YESNO)
    hidemonpic
    if(var(VAR_RESULT) == YES){
        call(Common_EventScript_GetGiftMonPartySlot)
        call(Common_EventScript_NameReceivedPartyMon)
    }
 
 
}
 
script NicknameBoxMon{
    msgbox("Give a nickname?", MSGBOX_YESNO)
    hidemonpic
    if(var(VAR_RESULT) == YES){
        call(Common_EventScript_NameReceivedBoxMon)
    }
    msgbox("Transferred to PC")
}
 
text NewLab_NotChosen{
    "You did not choose the Pokemon"
}

script InitiatePokedexSequenceAndRivalBattle{
	lock
    delay(20)
    applymovement(OBJ_EVENT_ID_PLAYER, TurnToProf)
    if(var(VAR_TEMP_0) == 1){
        setobjectxy(5, 5, 11)
        applymovement(4, MoveBallOne)
    }
    if(var(VAR_TEMP_0) == 2){
        setobjectxy(5, 6, 11)
        applymovement(4, MoveBallTwo)
    }
    if(var(VAR_TEMP_0) == 3){
        setobjectxy(5, 7, 11)
        applymovement(4, MoveBallThree)
    }    
	waitmovement(0)
    delay(20)
    call(GivePokedex)
    addobject(5)
    call(RivalBattleTrigger)
	release
    return
}
 
script GivePokedex{
    lock
    msgbox(TakePokedex, MSGBOX_DEFAULT)
    playfanfare(MUS_OBTAIN_ITEM)
	message("{PLAYER} received the POKéDEX!$")
	waitfanfare
    closemessage
	setflag(FLAG_SYS_POKEDEX_GET)
    setflag(FLAG_ADVENTURE_STARTED)
	special(SetUnlockedPokedexFlags)
	setflag(FLAG_RECEIVED_POKEDEX_FROM_BIRCH)
	setvar(VAR_CABLE_CLUB_TUTORIAL_STATE, 1)
    msgbox(LittlerootTown_ProfessorBirchsLab_Text_ExplainPokedex, MSGBOX_DEFAULT)
    msgbox("Also take these with you", MSGBOX_DEFAULT)
    giveitem(ITEM_POKE_BALL, 10)
    msgbox(format("These allow you to catch your own Pokemon in tall grass so you can collect research for the Pokedex"), MSGBOX_DEFAULT)
    msgbox(format("Take this brand new item as well and see the magic for yourself!"))
    giveitem(ITEM_POKE_VIAL)
    waitmessage
    release
	return
}
 
script UpgradeNationalDex{
    lock
    playfanfare(MUS_OBTAIN_ITEM)
	message(LittlerootTown_ProfessorBirchsLab_Text_PokedexUpgradedToNational)
	waitmessage
	waitfanfare
	setflag(FLAG_SYS_NATIONAL_DEX)
	special(EnableNationalPokedex)
    closemessage
    release
    return
}

movement MoveBallOne{
    walk_left * 2
}
movement MoveBallTwo{
    walk_left * 1
}
movement MoveBallThree{
    face_left
}
movement TurnToProf{
    face_right
}
 
text TakePokedex{
    "Here, {PLAYER}, I ordered this for my\n"
	"research, but I think you should have\l"
	"this POKéDEX."
}

script DontTouchThat{
	lockall
	msgbox("DONT TOUCH THAT")
	applymovement(OBJ_EVENT_ID_PLAYER, MoveAwayFromThere)
	waitmovement(0)
	releaseall
}
 
movement MoveAwayFromThere{
    walk_left * 2
}

// Rival Battle Scripts 
 
script RivalBattleTrigger{
    checkplayergender
    goto_if_eq(VAR_RESULT, MALE, MayBattle)
    goto_if_eq(VAR_RESULT, FEMALE, BrendanBattle)
}

script MayBattle{
    playbgm(MUS_ENCOUNTER_MAY, TRUE)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement(0)
    applymovement(5, RivalMoveUp)
    waitmovement(0)
    trainerbattle_single(TRAINER_MAY_ROUTE_103_TREECKO, RivalIntro, Route103_Text_MayDefeated, RivalBattleOver)
	end
}
 
script BrendanBattle{
    playbgm(MUS_ENCOUNTER_BRENDAN, TRUE)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement(0)
    applymovement(5, RivalMoveUp)
    waitmovement(0)
    trainerbattle_single(TRAINER_BRENDAN_ROUTE_103_TREECKO, RivalIntro, Route103_Text_BrendanDefeated, RivalBattleOver)
	end
}

movement RivalMoveUp{
    walk_up * 4
}
 
movement RivalLeave{
    walk_down * 5
}

script RivalBattleOver{
	lockall
    msgbox(format("Well that wasn't fun. I'll beat you next time."))
    applymovement(5, RivalLeave)
    waitmovement(0)
	setflag(RIVAL_STARTER_BATTLE)
	setvar(VAR_LITTLEROOT_STATE, 3) 
    removeobject(5)
	releaseall
}

text RivalIntro{
    "We'll See if you have what it takes!"
}